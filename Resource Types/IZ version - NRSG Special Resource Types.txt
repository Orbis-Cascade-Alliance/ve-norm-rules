rule "audiocd - LDR, 007, 3xx, 5xx"
priority 100
when
    (((existsControl "LDR.{6,1}.i|j") AND (not existsControl "008.{23,1}.o"))
    AND 
    ((existsControl "007.{0,7}.s*****g")
    OR
    ((exists "300.a.*disc*") AND (exists "300.c.*4 3/4*"))
    OR
    ((exists "337.a.*CD*") OR (exists "500.a.*CD*|*ompact disc*") OR (exists "538.a.*CD*|*ompact disc*"))))
then
    addField "972.a.audio_CDs" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "audiocassette - LDR, 007, 300"
priority 95
when
    (((existsControl "LDR.{6,1}.i|j") AND (not existsControl "008.{23,1}.o"))
    AND 
    ((existsControl "007.{0,2}.ss") OR (exists "300.a.*cassette*")))
then
    addField "972.a.audiocassettes" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "lprecord - LDR, 007, 300"
priority 90
when
    (((existsControl "LDR.{6,1}.i|j") AND (not existsControl "008.{23,1}.o"))
    AND 
    ((existsControl "007.{0,7}.sd****c|d|e|f") OR (exists "300.c.*16 in*|*12 in*|*10 in*|*7 in*")))
then
    addField "972.a.lprecords" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "videogame - LDR, 008"
priority 85
when
    #Computer file AND type of computer file = game AND not online form.
    ((existsControl "LDR.{06,1}.m") AND (existsControl "008.{26,1}.g") AND (not existsControl "008.{23,1}.o"))
then
    addField "972.a.videogames" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "cddvdrom - LDR, 008, 007"
priority 80
when
    (((not existsControl "LDR.{6,1}.c|d|e|f|g|k|o|r") AND (not existsControl "LDR.{6,2}.ab|ai|as") AND (existsControl "008.{23,1}.q") AND ((existsControl "007.{00,5}.c***g") OR (exists "300.a.*-ROM*")) AND (not existsControl "008.{29,1}.1") AND (not existsControl "008.{24,1}.m") AND (not exists "502"))
    OR
    ((existsControl "LDR.{6,1}.g") AND (existsControl "008.{29,1}.q") AND ((existsControl "007.{00,5}.c***g") OR (exists "300.a.*-ROM*")) AND (not existsControl "008.{33,1}.s|t") AND (not exists "502"))
    OR
    ((existsControl "LDR.{6,1}.k") AND (existsControl "008.{29,1}.q") AND ((existsControl "007.{00,5}.c***g") OR (exists "300.a.*-ROM*")) AND (not existsControl "008.{33,1}.i|k|l|n|s|t") AND (not exists "502"))
    OR
    ((existsControl "LDR.{6,1}.o") AND (existsControl "008.{29,1}.q") AND ((existsControl "007.{00,5}.c***g") OR (exists "300.a.*-ROM*")) AND (not exists "502")))
then
    addField "972.a.cddvdrom" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "microform - LDR, 008"
priority 75
when
    #Include language material, sound recordings, computer files, mixed materials, and manuscripts AND require microform form byte AND exclude conference proceedings AND exclude monographic serials AND exclude dissertations.
    (((not existsControl "LDR.{6,1}.e|f|g|k|o|r") AND (existsControl "008.{23,1}.a|b|c") AND (not existsControl "008.{29,1}.1") AND (not existsControl "008.{24,1}.m") AND (not exists "502"))
    OR
    #Cartographic material, projected media, 2-d nonprojectable graphic, kit, or 3-d artifact AND microform form AND exclude dissertations.
    ((existsControl "LDR.{6,1}.e|f|g|k|o|r") AND (existsControl "008.{29,1}.a|b|c") AND (not exists "502")))
then
    addField "972.a.microform" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "dvdvideo - 008, LDR, 007, 3xx, 5xx"
priority 70
when
    (((existsControl "008.{33,1}.v") AND (not existsControl "008.{29,1}.o"))
    AND 
    (((existsControl "LDR.{6,1}.g") AND (existsControl "007.{0,5}.v***v"))
    OR
    ((exists "300.a.*DVD*") OR (exists "347.b.*DVD*") OR (exists "538.a.*DVD*"))))
then
    addField "972.a.dvd_videos" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "blurayvideo - 008, LDR, 007, 3xx, 5xx"
priority 65
when
    (((existsControl "008.{33,1}.v") AND (not existsControl "008.{29,1}.o"))
    AND 
    (((existsControl "LDR.{6,1}.g") AND (existsControl "007.{0,5}.v***s"))
    OR
    ((exists "300.a.*blu-*|*Blu-*") OR (exists "347.b.*blu-*|*Blu-*") OR (exists "538.a.*blu-*|*Blu-*"))))
then
    addField "972.a.blurayvideos" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "videocassette - 008, LDR, 007, 3xx, 5xx"
priority 60
when
    (((existsControl "008.{33,1}.v") AND (not existsControl "008.{29,1}.o")) 
    AND 
    (((existsControl "LDR.{6,1}.g") AND (existsControl "007.{0,2}.vf"))
    OR
    ((exists "300.a.*VHS*") OR (exists "346.a.*VHS*") OR (exists "538.a.*VHS*"))))
then
    addField "972.a.videocassettes" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "laserdisc - 008, LDR, 007, 3xx, 5xx"
priority 55
when
    (((existsControl "008.{33,1}.v") AND (not existsControl "008.{29,1}.o"))
    AND 
    (((existsControl "LDR.{6,1}.g") AND (existsControl "007.{0,5}.v***g"))
    OR
    ((exists "300.a.*laserdisc*") OR (exists "500.a.*laserdisc*") OR (exists "538.a.*laserdisc*"))))
then
    addField "972.a.laserdiscs" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "pbook - LDR, 008, 502"
priority 50
when
    #nonserial print, OR monographic serials OR non-thesis monographs
    (((existsControl "LDR.{06,2}.aa|ac|ad") 
    OR 
    ((existsControl "LDR.{06,2}.ab|ai|as") AND (existsControl "008.{21,1}.m")) 
    OR 
    ((existsControl "LDR.{06,2}.am") AND (not existsControl "008.{24,1}.m")))
    AND 
    #exclude microform, online, electronic form codes, AND exclude conference proceedings, AND exclude dissertations AND exclude records already marked.
    ((not existsControl "008.{23,1}.a|b|c|o|q|s") AND (not existsControl "008.{29,1}.1") AND (not exists "502")))
then
    addField "972.a.pbooks" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end

rule "computerfile - LDR, 008"
priority 45
when
    #exclude all record types except nonserial language material, sound recordings, computer files, mixed materials, and manuscripts AND require direct electronic form byte AND exclude electronic resource with dimension of 4 3/4 in. in 007 AND exclude conference proceedings AND exclude monographic serials AND exclude dissertations.
    (((not existsControl "LDR.{6,1}.c|d|e|f|g|k|o|r") AND (not existsControl "LDR.{6,2}.ab|ai|as") AND (existsControl "008.{23,1}.q") AND (not existsControl "007.{00,5}.c***g") AND (not existsControl "008.{29,1}.1") AND (not existsControl "008.{24,1}.m") AND (not exists "502"))
    OR
    #Projected media AND direct electronic form AND exclude electronic resource with dimension of 4 3/4 in. in 007 AND not slides or transparencies (TMat) AND exclude dissertations.
    ((existsControl "LDR.{6,1}.g") AND (existsControl "008.{29,1}.q") AND (not existsControl "007.{00,5}.c***g") AND (not existsControl "008.{33,1}.s|t") AND (not exists "502"))
    OR
    #2-D nonprojectable graphic AND direct electronic form AND exclude electronic resource with dimension of 4 3/4 in. in 007 AND not picture, graphic, technical drawing, chart, slide, or transparency AND exclude dissertations.
    ((existsControl "LDR.{6,1}.k") AND (existsControl "008.{29,1}.q") AND (not existsControl "007.{00,5}.c***g") AND (not existsControl "008.{33,1}.i|k|l|n|s|t") AND (not exists "502"))
    OR
    #Kit AND direct electronic form AND exclude electronic resource with dimension of 4 3/4 in. in 007 AND exclude dissertations.
    ((existsControl "LDR.{6,1}.o") AND (existsControl "008.{29,1}.q") AND (not existsControl "007.{00,5}.c***g") AND (not exists "502")))
then
    addField "972.a.computer_files" if (not exists "972")
    addSubField "972.9.LOCAL" if (not exists "972.9.LOCAL")
end
