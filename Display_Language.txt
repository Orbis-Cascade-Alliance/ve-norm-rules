rule "Primo VE - Language 008 no 546"
#  041 subfields displayed and searched in language field in PBO: a,d,i,j,p,q,r
# 4/1/22: VE OTB language facet and search fields include 041 subfields a, b, d, e, f, g, j OR 008/35-37
# This field should be combined with Language (Local) field for display
# Language (Local) displays 546 language note
# This field has been edited to display 008/041 languages only IF
# 1) No 546 exists
# 2) 546 contains a pattern that indicates that it does not name the language of the resource
# a) variations on "closed captions"
# b) begins with "translated from"
# Example OCLC numbers
# 02164044 008 only
# 42686496 546 captioned
# 1158419826 translated
# 60556363 translated from in middle of 546
# 07269975 no 546
# 31523613 no 546
# 53033697 has 546
# 38328628 has 546
# 42690654 outlier 2 546 fields; 1 = captioned
	when
        MARC.control is "008" AND NOT
		MARC is "546"
	then
		set TEMP"2" to MARC.control."008".Language
		return list using regex (TEMP"1",TEMP"2","[a-z]{3}")
		normalize discovery lang (TEMP"1")
        create pnx."display"."language" with list TEMP"1"
end

rule "Primo VE - Language 008 with 546 exceptions"
	when
        MARC.control is "008" AND 
		MARC."546" has any "a" AND 
		(MARC."546"."a" match "(?i)close(d)?[ -]caption(ed|ing|s)(\\.)?" OR
		MARC."546"."a" match "Translated from .*")
	then
		set TEMP"2" to MARC.control."008".Language
		return list using regex (TEMP"1",TEMP"2","[a-z]{3}")
		normalize discovery lang (TEMP"1")
        create pnx."display"."language" with list TEMP"1"
end

rule "Primo VE- Language 041 a no 546"
	when
        MARC "041" has any "a,d,e,i,j,p,q,r" AND NOT
		MARC is "546"
	then
	    set TEMP"2" to MARC."041" subfields "a,d,e,i,j,p,q,r"
        lower case (TEMP"2")
	    normalize discovery language (TEMP"1",TEMP"2")
        create pnx."display"."language" with list TEMP"1"
end

rule "Primo VE- Language 041 a with 546 exceptions"
	when
        MARC "041" has any "a,d,e,i,j,p,q,r" AND
		MARC."546" has any "a" AND 
		(MARC."546"."a" match "(?i)close(d)?[ -]caption(ed|ing|s)(\\.)?" OR
		MARC."546"."a" match "Translated from .*")
	then
	    set TEMP"2" to MARC."041" subfields "a,d,e,i,j,p,q,r"
        lower case (TEMP"2")
	    normalize discovery language (TEMP"1",TEMP"2")
        create pnx."display"."language" with list TEMP"1"
end